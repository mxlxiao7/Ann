/**
 * CheckStyle
 */
apply plugin: 'checkstyle'
checkstyle {
    toolVersion = "8.2"
}
task checkStyle(type: Checkstyle) {
    ignoreFailures = true // 忽略错误继续构建
    classpath = files()
    configFile file("${project.projectDir}/checkstyle/checkstyle.xml")
    configProperties.checkstyleSuppressionsPath = file("${project.projectDir}/checkstyle/suppressions.xml").absolutePath
    source 'src'
    include '**/*.java'
    exclude '**/gen/**', '**/test/java/**'
    reports { // 支持html和xml两种报告形式，可以任选其一(html更具有可读性)
        xml.enabled = false
        html.enabled = true
//        html.stylesheet resources.text.fromFile("${project.projectDir}/checkstyle/checkstyle-noframes-sorted.xml")
        html.destination file("$reportsDir/checkstyle/checkstyle.html")
    }
}


/**
 * PMD
 */
apply plugin: 'pmd'
task pmd(type: Pmd) {
    ignoreFailures = true
    ruleSets = [
            'java-android',
            'java-basic',
            'java-braces',
            'java-clone',
            'java-codesize',
            //'java-comments',
            'java-controversial',
            'java-coupling',
            'java-design',
            'java-empty',
            'java-finalizers',
            'java-imports',
            'java-j2ee',
            'java-javabeans',
            'java-junit',
            'java-logging-jakarta-commons',
            'java-logging-java',
            'java-migrating',
            'java-naming',
            'java-optimizations',
            'java-strictexception',
            'java-strings',
            'java-sunsecure',
            'java-typeresolution',
            'java-unnecessary',
            'java-unusedcode'
    ]
    source = fileTree('src')
    reports {
        xml.enabled = false
        html.enabled = true
        html.destination file("$reportsDir/pmd/pmd.html")
    }
}


check.dependsOn 'checkStyle', 'pmd', 'lint'